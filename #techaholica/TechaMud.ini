[script]
n0=/*
n1=TechaMud
n2=A Multi User Dungeon (MUD) text based dungeon crawler except instead of elixers and goblins, you have alcohol and bouncers.
n3=
n4=- created by Techaholica http://techaholica.com
n5=
n6=*/
n7=
n8=
n9=on *:LOAD: {
n10=  setVariables
n11=  createBuffer
n12=  startTimers
n13=}
n14=
n15=on *:JOIN:#:{
n16=  startUserTimer
n17=}
n18=
n19=on *:PART:#:{
n20=  stopUserTimer
n21=}
n22=
n23=
n24=alias -l startUserTimer {
n25=  timer $+ $nick 0 %userTimer setUserStats $nick
n26=}
n27=
n28=alias -l stopUserTimer {
n29=  timer $+ $nick off
n30=
n31=  // now flip the switch to make them not in game anymore
n32=  var %userFile $+(%baseDir,\users\,$nick,.ini)
n33=  writeini %userFile character ingame no
n34=}
n35=
n36=alias -l setUserStats {
n37=  setStats $1 cash %userTimerCashReward
n38=  setStats $1 drunk %userTimerSoberReward
n39=}
n40=
n41=alias -l startTimers {
n42=  timerBufferWriter -m 0 %bufferTimer writeFromBuffer
n43=  timerCurrentSong 0 5 checkNewSong
n44=}
n45=
n46=alias -l setStats {
n47=  var %userFile $+(%baseDir,\users\,$1,.ini)
n48=  if ($exists(%userFile)) {
n49=    var %alive $readini(%userFile, n, character, alive)
n50=
n51=    if (%alive == yes) {
n52=      var %property $readini(%userFile, n, character, $2)
n53=      var %result $calc(%property + $3)
n54=
n55=      if (%result < 0) {
n56=        // shouldn't be any values that go below 0
n57=        var %result 0
n58=      }
n59=
n60=      writeini %userFile character $2 %result
n61=    }
n62=  }
n63=}
n64=
n65=/*
n66=Can probably get rid of this if we use setStats to achive the same thing
n67=*/
n68=alias -l addCash {
n69=  var %userFile $+(%baseDir,\users\,$1,.ini)
n70=
n71=  // only add cash if user is in game and alive  
n72=  if ($exists(%userFile)) {
n73=    var %alive $readini(%userFile, n, character, alive)
n74=
n75=    if (%alive == yes) {
n76=      var %cash $readini(%userFile, n, character, cash)
n77=      if ($2) {
n78=        inc %cash $2
n79=      }
n80=      else {
n81=        inc %cash %cashAddAmount
n82=      }
n83=
n84=      writeini %userFile character cash %cash
n85=    }
n86=  } 
n87=}
n88=
n89=alias -l checkNewSong {
n90=  var %currentSong $+(%baseDir,\nowplaying.txt)
n91=  var %dj $+(%baseDir,\objects\DJ.empirerecords.ini)
n92=  var %song $read(%currentSong ,n,1)
n93=  var %description A girl in a plaid skirt wearing sweater with her midriff showing says "I'm currently playing: %song $+ "
n94=  writeini %dj object description %description 
n95=}
n96=
n97=alias -l setVariables {
n98=  // mix of variables from config and manually set
n99=  // if you have this bot on multiple channel, it will fail because the Varaible file is vars.ini for both
n100=  // you should either write your variables to a different vars.ini file or change the below variables to 
n101=  // do something like this %gid#myotherchannel
n102=  // that way each channel has its own set of global variables
n103=
n104=  Set -e %channel #techaholica
n105=  Set -e %classFile TechaMud\#techaholica\classes\classes.ini
n106=  Set -e %listFile TechaMud\#techaholica\locations\locations.ini
n107=  Set -e %bufferCurrent 0
n108=  Set -e %bufferNext 0
n109=  Set -e %techaDrinks 0
n110=
n111=  Set -e %gid $readini(TechaMud\#techaholica\TechaMud.config, n, config, gid) 
n112=  Set -e %gameTitle $readini(TechaMud\#techaholica\TechaMud.config, n, config, gameTitle)
n113=  Set -e %welcomeMessage $readini(TechaMud\#techaholica\TechaMud.config, n, config, welcomeMessage)
n114=  Set -e %rollCharacterMessage $readini(TechaMud\#techaholica\TechaMud.config, n, config, rollCharacterMessage)
n115=  Set -e %bufferMax $readini(TechaMud\#techaholica\TechaMud.config, n, config, bufferMax)
n116=  Set -e %cashAddAmount $readini(TechaMud\#techaholica\TechaMud.config, n, config, cashAddAmount)
n117=  Set -e %baseDir $readini(TechaMud\#techaholica\TechaMud.config, n, config, baseDir)minPosDrunk=5
n118=  Set -e %maxPosDrunk $readini(TechaMud\#techaholica\TechaMud.config, n, config, maxPosDrunk)
n119=  Set -e %minPosDrunk $readini(TechaMud\#techaholica\TechaMud.config, n, config, minPosDrunk)
n120=  Set -e %minNegDrunk $readini(TechaMud\#techaholica\TechaMud.config, n, config, minNegDrunk)
n121=  Set -e %maxNegDrunk $readini(TechaMud\#techaholica\TechaMud.config, n, config, maxNegDrunk)
n122=  Set -e %minPosSober $readini(TechaMud\#techaholica\TechaMud.config, n, config, minPosSober)
n123=  Set -e %maxPosSober $readini(TechaMud\#techaholica\TechaMud.config, n, config, maxPosSober)
n124=  Set -e %minNegSober $readini(TechaMud\#techaholica\TechaMud.config, n, config, minNegSober)
n125=  Set -e %maxNegSober $readini(TechaMud\#techaholica\TechaMud.config, n, config, maxNegSober)
n126=  Set -e %bufferTimer $readini(TechaMud\#techaholica\TechaMud.config, n, config, bufferTimer)
n127=  Set -e %userTimer $readini(TechaMud\#techaholica\TechaMud.config, n, config, userTimer)
n128=  Set -e %userTimerCashReward $readini(TechaMud\#techaholica\TechaMud.config, n, config, userTimerCashReward)
n129=  Set -e %userTimerSoberReward $readini(TechaMud\#techaholica\TechaMud.config, n, config, userTimerSoberReward)
n130=  Set -e %inventoryMax $readini(TechaMud\#techaholica\TechaMud.config, n, config, inventoryMax)
n131=  Set -e %freeDrinkNumber $readini(TechaMud\#techaholica\TechaMud.config, n, config, freeDrinkNumber)
n132=  Set -e %freeDrinkNumberSlots $readini(TechaMud\#techaholica\TechaMud.config, n, config, freeDrinkNumberSlots)
n133=
n134=}
n135=
n136=alias -l writeFromBuffer {
n137=  if ($($+(%,buffer.,%bufferCurrent),2) != $null) {
n138=    msg %channel $($+(%,buffer.,%bufferCurrent),2)
n139=    set %buffer. $+ %bufferCurrent $null
n140=    inc %bufferCurrent
n141=
n142=    if (%bufferCurrent == %bufferMax) {
n143=      set %bufferCurrent 0
n144=    }
n145=  }
n146=}
n147=
n148=alias -l writeMsg {
n149=  if ($1 != $null) {
n150=    writeToBuffer $+(%gid,:,$nick,:,$chr(32),$1-)
n151=  }
n152=}
n153=
n154=alias -l writeToBuffer {
n155=
n156=  if ($1 != $null) {
n157=    set %buffer. $+ %bufferNext $1-
n158=    inc %bufferNext
n159=
n160=    if (%bufferNext == %bufferMax) {
n161=      set %bufferNext 0
n162=    }
n163=  }
n164=}
n165=
n166=// initialize the buffer
n167=alias -l createBuffer {
n168=  var %i 0
n169=
n170=  while (%i < %bufferMax) {
n171=    set %buffer. $+ %i $null
n172=    inc %i
n173=  }
n174=}
n175=
n176=
n177=alias -l listClasses {
n178=
n179=  if ($1 == $null) {
n180=
n181=    var %ini = $ini(%classFile,0)
n182=    var %x = 1
n183=    var %topic
n184=    var %output Classes: 
n185=
n186=    while (%x <= %ini) {
n187=      %topic = $ini(%classFile,%x)
n188=      if (%x == 1) {
n189=        var %output $+(%output,$chr(32),%topic)
n190=      } 
n191=      else {
n192=        var %output $+(%output,$chr(44),$chr(32),%topic)
n193=      }
n194=
n195=      inc %x
n196=    }
n197=
n198=    writeMsg %output
n199=  }
n200=  else {
n201=    var %about $readini(%classFile, n, $1-, about)
n202=    if (%about != $null) {
n203=      writeMsg %about
n204=    } 
n205=    else {
n206=      writeMsg $+(Class,$chr(32),$1,$chr(32), does not exist)
n207=    }
n208=  }
n209=
n210=}
n211=
n212=alias -l setClass {
n213=
n214=  var %name $readini(%classFile, n, $1, name)
n215=  if (%name == $null) {
n216=    writeMsg $+($1,$chr(32), Class does not exist )
n217=  } 
n218=  else {
n219=    var %sex $readini(%classFile, n, $1, sex)
n220=    var %tolerance $readini(%classFile, n, $1, tolerance)
n221=    var %strength $readini(%classFile, n, $1, strength)
n222=    var %appearance $readini(%classFile, n, $1, appearance)
n223=    var %intelligence $readini(%classFile, n, $1, intelligence)
n224=    var %cash $readini(%classFile, n, $1, cash)
n225=    var %starting $readini(%classFile, n, $1, starting)
n226=
n227=    if (($sex == null) || (%tolerance == $null) || (%strength == $null) || (%appearance == $null) || (%intelligence == $null)) {
n228=      writeMsg $+(Error creating class..., $1)
n229=    } 
n230=    else {
n231=      var %userFile $+(%baseDir,\users\,$nick,.ini)
n232=      writeini %userFile character class $1
n233=      writeini %userFile character tolerance %tolerance
n234=      writeini %userFile character sex %sex
n235=      writeini %userFile character strength %strength
n236=      writeini %userFile character appearance %appearance
n237=      writeini %userFile character intelligence %intelligence
n238=
n239=      // setup an except for techadrunk so he has enough money to drink
n240=      if ($nick == Techadrunk) {
n241=        writeini %userFile character cash 1000
n242=      } 
n243=      else {
n244=        writeini %userFile character cash %cash
n245=      }
n246=      writeini %userFile character alive yes
n247=      writeini %userFile character location Street
n248=      writeini %userFile character items 0
n249=      writeini %userFile character ingame yes
n250=      writeini %userFile character bet 1
n251=
n252=      remini %userFile inventory
n253=      remini %userFile wardrobe
n254=
n255=      writeMsg $+(You are now a,$chr(32), $1,$chr(32),%starting)
n256=    }
n257=  }
n258=}
n259=
n260=alias -l checkUser {
n261=  var %userFile $+(%baseDir,\users\,$nick,.ini)
n262=
n263=  if $isfile(%userFile) {
n264=    var %ingame $readini(%userFile, n, character, ingame)
n265=    var %alive $readini(%userFile , n, character, alive)
n266=    if ((%ingame == yes) && (%alive == yes)) {
n267=      return $true
n268=    }  
n269=    else {
n270=      writeMsg You must be in the game (!startGame) and alive (!roll CLASS) to use this commmand
n271=      return $false
n272=    }
n273=  } 
n274=  else {
n275=    writeMsg You must be in the game (!startGame) and alive (!roll CLASS) to use this commmand
n276=    return $false
n277=  }
n278=}
n279=
n280=alias -l listLocations {
n281=  var %ini $ini(%listFile ,0)
n282=  var %x 1
n283=  var %topic
n284=  var %output Locations: 
n285=
n286=  while (%x <= %ini) {
n287=    %topic = $ini(%listFile,%x)
n288=    if (%x == 1) {
n289=      var %output $+(%output,$chr(32),%topic)
n290=    } 
n291=    else {
n292=      var %output $+(%output,$chr(44),$chr(32),%topic)
n293=    }
n294=    inc %x
n295=  }
n296=
n297=  writeMsg %output
n298=}
n299=
n300=alias -l useMoney {
n301=  if ($1 == $null) {
n302=    // should never get here
n303=    writeMsg Need to specify money amount
n304=    return $false
n305=  } 
n306=  else {
n307=    var %price $1
n308=
n309=    if (%price isnum) {
n310=      var %userFile $+(%baseDir,\users\,$nick,.ini)
n311=      var %cash $readini(%userFile , n, character, cash)
n312=
n313=      if (%cash < %price) {
n314=        writeMsg You don't have enough money for that.
n315=        return $false
n316=      } 
n317=      else {
n318=        dec %cash %price
n319=        writeini %userFile character cash %cash
n320=        return $true
n321=      }
n322=    } 
n323=    else {
n324=      writeMsg $+(%price, $chr(32), is not a number)
n325=    }
n326=  }
n327=}
n328=
n329=alias -l travelTo {
n330=
n331=  if ($1 == $null) {
n332=    // shouldn't ever get here since this is an internal command, but whatevs
n333=    writeMsg Error moving to null location
n334=  } 
n335=  else {
n336=    var %userFile $+(%baseDir,\users\,$nick,.ini)
n337=    var %loc $readini(%userFile, n, character, location)
n338=
n339=    if (%loc == $1) {
n340=      writeMsg $+(You are already at:, $chr(32), %loc)
n341=    } 
n342=    else {
n343=      var %name $readini(%listFile , n, $1, name)
n344=
n345=      if (%name == $null) {
n346=        writeMsg $+(Location does not exist: , $1)
n347=      } 
n348=      else {
n349=        var %cover $calc($readini(%listFile , n, $1, cover))
n350=
n351=        if (%cover == 0) {
n352=          writeMsg $+(You make your way to:, $chr(32), $1)
n353=          writeini %userFile character location $1
n354=        } 
n355=        elseif ($2 == $null) {
n356=          writeMsg $+(There is a $, %cover, $chr(32), cover here. Use !go [location] [amount] to enter)
n357=        } 
n358=        elseif ($2 isnum) {
n359=          if ($2 < %cover) {
n360=            writeMsg $+(That is not enough money. There is a $, %cover, $chr(32), cover here. Use !go [location] [amount] to enter)
n361=          }
n362=          else {
n363=            var %paid $useMoney($2)
n364=
n365=            if (%paid) {
n366=              writeMsg $+(You make your way to:, $chr(32), $1)
n367=              writeini %userFile character location $1
n368=            } 
n369=          }
n370=        } 
n371=        else {
n372=          writeMsg $+($2, $chr(32), is not a number)
n373=        } 
n374=      }
n375=    }
n376=  }
n377=}
n378=
n379=alias -l addInventoryItem {
n380=  if ($1 == $null) {
n381=    writeMsg You must specify an item to add to your inventory
n382=  } 
n383=  else {
n384=    var %userFile $+(%baseDir,\users\,$nick,.ini)
n385=    var %items $readini(%userFile , n, character, items)
n386=
n387=    inc %items
n388=    writeini %userFile character items %items
n389=    writeini %userFile inventory %items $1
n390=  } 
n391=}
n392=
n393=alias -l getItem {
n394=  if ($1 != $null ) {
n395=    var %userFile $+(%baseDir,\users\,$nick,.ini)
n396=    var %items $readini(%userFile, n, character, items)
n397=    var %i 1
n398=
n399=    while (%i <= %items ) {
n400=      var %itemFile $readini(%userFile , n, inventory, %i)
n401=      var %short $readini(%itemFile, n, object, short)
n402=
n403=      if (%short == $1) {
n404=        // if we find the item, return the item file
n405=        return %itemFile
n406=      }
n407=
n408=      inc %i
n409=    }
n410=
n411=    // if we go through all the items and don't find what we are looking for, return $null
n412=    return $null
n413=  } 
n414=  else {
n415=    writeMsg You must specify an item to check in your inventory
n416=  }
n417=}
n418=
n419=alias -l drink {
n420=  // some logic for the TechaDrunk bot
n421=  if ($nick == Techadrunk) {
n422=    inc %techaDrinks
n423=    write -c TechaDrinks.txt %techaDrinks
n424=    addInventoryItem $1
n425=  }
n426=
n427=  if ($1 != $null) {
n428=    var %drinkable $readini($1, n, item, drinkable)
n429=
n430=    if (%drinkable == yes) {
n431=      var %alcohol $readini($1, n, object, alcohol)
n432=      var %strength $readini($1, n, stats, strength)
n433=
n434=      var %succesfulDrink $updateDrunk(%alcohol, %strength)
n435=      if (%succesfulDrink) {
n436=        removeItem $1
n437=      }
n438=
n439=    } 
n440=    else {
n441=      writeMsg You can't drink that.
n442=    }
n443=  } 
n444=  else {
n445=    writeMsg You must specify something to drink
n446=  }
n447=}
n448=
n449=alias -l eat {
n450=
n451=  if ($1 != $null) {
n452=    var %edible $readini($1, n, item, edible)
n453=
n454=    if (%edible == yes) {
n455=      var %strength $readini($1, n, stats, strength)
n456=
n457=      // updateDrunk takes a yes|no as first peramted to see if its alcoholic
n458=      // no alcoholic food..for now
n459=      var %succesfulEat $updateDrunk(no, %strength)
n460=      if (%succesfulEat) {
n461=        removeItem $1
n462=      }
n463=
n464=    } 
n465=    else {
n466=      writeMsg You can't eat that.
n467=    }
n468=  } 
n469=  else {
n470=    writeMsg You must specify something to eat
n471=  }
n472=}
n473=
n474=
n475=/*
n476=- rolling is 1-100. 1-50 is a positive, 51+ is a negative.  Adding or subtracting from 100 gives you worse or better odds at something positive
n477=- for adding drunk points, positive is adding 1-5 points, negative is 6-15 points
n478=- for getting sober positive is removing 6-10 points, and negative is removing 1-5 points
n479=- it is easier to get drunk than it is to get sober
n480=- getting 100 drunk kills you :(
n481=*/
n482=
n483=alias -l updateDrunk {
n484=  // $1 = alcohol (yes|no) $2 = strength of item
n485=  if (($1 != $null) && ($2 != $null)) {
n486=    var %userFile $+(%baseDir,\users\,$nick,.ini)
n487=    var %drunk $readini(%userFile , n, character, drunk)
n488=    var %tolerance $readini(%userFile , n, character, tolerance)
n489=    var %points 0
n490=
n491=    if (($1 == no) && (%drunk == 0)) {
n492=      writeMsg There is no need to consume the item. You are already completely sober.
n493=      return $false
n494=    } 
n495=    elseif ($1 == no) {
n496=      // this is a non alcoholic item so see how many drunk points to remove
n497=      // first add the item strength to our tolerance
n498=      inc %tolerance $2
n499=      // subtract the tolerance from 100 to reduce the pool of numbers thus making getting a positive score more likely
n500=      var %maxNumber $calc(100 - %tolerance)
n501=
n502=      // get a random number from 1-max number. 1-50 is positive
n503=      var %result $rand(1,%maxNumber)
n504=
n505=      if (%result <= 50) {
n506=        // this is a positive outcome so remove from points from the drunk stat
n507=        var %points $rand(%minPosSober,%maxPosSober)
n508=        var %quantifier much
n509=      } 
n510=      else {
n511=        // this is a negative result so only remove a small amount of points
n512=        var %points $rand(%minNegSober,%maxNegSober)  
n513=        var %quantifier slightly
n514=      }
n515=
n516=      dec %drunk %points 
n517=      if (%drunk < 0) {
n518=        // can't get negative drunk
n519=        var %drunk 0
n520=      }
n521=
n522=      writeini %userFile character drunk %drunk
n523=      writeMsg $+(You consume the item and feel,  $chr(32), %quantifier, $chr(32), more sober. New drunk level:, $chr(32), %drunk)
n524=
n525=      return $true
n526=    } 
n527=    elseif ($1 == yes) {
n528=      // this is a an alcoholic item so see how many drunk points to add
n529=      // first subtract the item strength from our tolerance
n530=      dec %tolerance $2
n531=
n532=      if (%tolerance < 0 ) {
n533=        // can't have a negative tolerance
n534=        var %tolerance 0
n535=      }
n536=
n537=      // add the tolerance to 100 to increase the pool of numbers thus making getting a negative score more likely
n538=      var %maxNumber $calc(100 + %tolerance)
n539=
n540=      // get a random number from 1-max number. 1-50 is positive
n541=      var %result $rand(1,%maxNumber)
n542=
n543=      if (%result <= 50) {
n544=        // this is a positive outcome so add a few points to the drunk stat
n545=        var %points $rand(%minPosDrunk,%maxPosDrunk)
n546=        var %quantifier somewhat
n547=      } 
n548=      else {
n549=        // this is a negative result so add a lot of points
n550=        var %points $rand(%minNegDrunk, %maxNegDrunk)  
n551=        var %quantifier much
n552=      }
n553=
n554=      inc %drunk %points 
n555=      if (%drunk >= 100) {
n556=        // You die :(
n557=        killCharacter alcohol poisoning
n558=        return $false
n559=      } 
n560=      else {
n561=        writeini %userFile character drunk %drunk
n562=        writeMsg $+(You consume the beverage and feel,  $chr(32), %quantifier, $chr(32), more drunk. New drunk level:, $chr(32), %drunk)
n563=
n564=        // some logic for the TechaDrunk bot
n565=        if ($nick == TechaDrunk) {
n566=          write -c TechaDrunk.txt %drunk
n567=        }
n568=
n569=        return $true
n570=      }
n571=    } 
n572=    else {
n573=      writeMsg Error updating drunk status: Unknown alcohol type
n574=      return $false
n575=    }
n576=  } 
n577=  else {
n578=    writeMsg Error updating drunk status
n579=    return $false
n580=  }
n581=}
n582=
n583=/*
n584=Loop through the items and keep an array of all the items in the list
n585=if the item is found, remove the item and decrement the overall item count
n586=after all the inventory has been put into the array, write back to the inventory list
n587=using new inventory slot numbers
n588=*/
n589=alias -l removeItem {
n590=  if ($1 != $null) {
n591=    var %itemArray
n592=    var %found $false
n593=    var %userFile $+(%baseDir,\users\,$nick,.ini)
n594=    var %items $readini(%userFile, n, character, items)
n595=    var %i 1
n596=    var %index 1
n597=
n598=    while (%i <= %items ) {
n599=      var %itemFile $readini(%userFile , n, inventory, %i)
n600=
n601=      if ((%itemFile == $1) && (%found == $false)) {
n602=        // flag that we found the item
n603=        var %found $true
n604=      } 
n605=      else {
n606=        // if the items don't match, catalogue the item
n607=        var %itemArray. $+ %index %itemFile
n608=        inc %index
n609=      }
n610=      inc %i
n611=    }
n612=    if (%found) {
n613=      // at this point we have all the items of the inventory minus the one item
n614=      // add all the items back to inventory and write the new item count
n615=      dec %items
n616=      remini %userFile inventory
n617=      var %x 1
n618=      while (%x <= %items ) {
n619=        var %addItem $($+(%,itemArray.,%x),2)
n620=        writeini %userFile inventory %x %addItem 
n621=        inc %x
n622=      }
n623=      writeini %userFile character items %items
n624=    } 
n625=    else {
n626=      writeMsg Could not find item to remove: $+ $1
n627=    }
n628=  } 
n629=  else {
n630=    writeMsg You must specify something to remove
n631=  }
n632=}
n633=
n634=alias -l killCharacter {
n635=  var %reason $1-
n636=  var %userFile $+(%baseDir,\users\,$nick,.ini)
n637=  var %class $readini(%userFile, n, character, class)
n638=  var %deathMsg $readini(%classFile, n, %class, death)
n639=
n640=  writeini %userFile character alive no
n641=
n642=  writeMsg $+(%deathMsg,$chr(32),Reason:,$chr(32), %reason)
n643=
n644=  // add some stupid logic to bring TechaDrunk back to life
n645=  if ($nick == Techadrunk) {
n646=    writeini %userFile character alive yes
n647=    writeini %userFile character cash 1000
n648=    writeini %userFile character drunk 0
n649=    write -c TechaDrunk.txt 0
n650=    write -c TechaDrinks.txt 0
n651=    Set %techaDrinks 0
n652=
n653=    writeMsg False alarm. You just black out!
n654=  }
n655=}
n656=
n657=/*
n658=$1= game name
n659=$2 = user to challenge 
n660=$3 = location
n661=$4 = objectfile
n662=*/
n663=alias -l playChallengedGame {
n664=  var %challengedFile  $+(%baseDir,\users\,$2,.ini)
n665=
n666=  if isfile(%challengedFile) {
n667=    var %challengedLocation $readini(%challengedFile, n, character, location)
n668=    if (%challengedLocation != $3) {
n669=      writeMsg User must be in the same room to play
n670=      } else {
n671=      // challenger (orig user) vs challenged
n672=      // both users pick a random number from 1 - max availabe
n673=      // max = 100 - drunk + intelligence
n674=      // user with highest score wins
n675=      var %origUserFile $+(%baseDir,\users\,$nick,.ini)
n676=      var %origDrunk $readini(%origUserFile , n, character, drunk)
n677=      var %origIntelligence $readini(%origUserFile , n, character, intelligence)
n678=      var %origUserScore $calc(100 - %origDrunk + %origIntelligence)
n679=
n680=      var %challengedDrunk $readini(%challengedFile, n, character, drunk)
n681=      var %challengedIntelligence $readini(%challengedFile, n, character, intelligence)
n682=      var %challengedUserScore $calc(100 - %challengedDrunk + %challengedIntelligence )
n683=
n684=      var %origRandom $rand(1, %origUserScore)
n685=      var %challengedRandom $rand(1, %challengedUserScore)
n686=
n687=      if (%origRandom > %challengedRandom ) {
n688=        var %victoryMessage $readini($4, n, object, victoryMessage)
n689=        writeMsg %victoryMessage
n690=      } 
n691=      else {
n692=        var %chanceofdeath $readini($4, n, object, chanceofdeath)
n693=        var %defeatMessage $readini($4, n, object, defeatMessage)
n694=        if (%chanceofdeath == 0) {
n695=          writeMsg %defeatMessage 
n696=        } 
n697=        else {
n698=          var %deathMessage $readini($4, n, object, deathMessage)
n699=          var %total $calc(100 - %origDrunk - %chanceofdeath)
n700=
n701=          if (%total <= 1) {
n702=            // anything 1 or below is auto-death
n703=            killCharacter %deathMessage
n704=          }
n705=          else {
n706=            var %random $rand(1, %total)
n707=            if (%random == 1) {
n708=              killCharacter %deathMessage
n709=            } 
n710=            else {
n711=              writeMsg %defeatMessage
n712=            }
n713=          }
n714=        }
n715=      }
n716=    }
n717=  } 
n718=  else {
n719=    writeMsg $+($1, $chr(32), does not exist)
n720=  }
n721=}
n722=
n723=/*
n724=- We will roll 9 times, each roll has 50/50 chance of getting positve or negative roll
n725=- positive: 40, 50, 100 | weighted 40x6 50x3 100x1
n726=- negative: 0, 10, 20, 30 | weighted 0x1 10x2 20x3 30x4
n727=- chance equal to 100 - drunk + intelligence
n728=- add up the 9 rolls for total score
n729=*/
n730=alias -l playSkeeball {
n731=  var %skeeballFile $+(%baseDir,\objects\skeeball.thedive.ini)
n732=  var %userFile $+(%baseDir,\users\,$nick,.ini)
n733=  var %drunk  $readini(%userFile, n, character, drunk)
n734=  var %intelligence $readini(%userFile, n, character, intelligence)
n735=  var %chance $calc(100 + %drunk - %intelligence)
n736=
n737=  // there is a cap on how much your intelligence affects you
n738=  // there should always be a chance at a negative
n739=  if ( %chance < 51) var %chance 51
n740=
n741=  var %score 0
n742=  // roll 9 times
n743=  var %x 1
n744=  while (%x <= 9) {
n745=    var %random $rand(1, %chance)
n746=    var %ballThrow $rand(1, 10)
n747=
n748=    if (%random <= 50) {
n749=      // this is a positive outcome
n750=      // ballThrow score meanings: 1-6 = 40, 7-9 = 50, 10 = 100
n751=      if (%ballThrow <= 6) {
n752=        inc %score 40
n753=      } 
n754=      elseif ((%ballThrow > 6) && (%ballThrow <= 9)) {
n755=        inc %score 50
n756=      } 
n757=      else {
n758=        // you rolled a hundo then!
n759=        inc %score 100
n760=      }
n761=    } 
n762=    else {
n763=      // this is a negative throw
n764=      // ballThrow score meanings: 1 = 0, 2 -3 = 10, 4-6 = 20, 7-10 = 30
n765=      if (%ballThrow == 1) {
n766=        // do nothing
n767=      } 
n768=      elseif ((%ballThrow > 1) && (%ballThrow <= 3 )) {
n769=        inc %score 10
n770=      }
n771=      elseif ((%ballThrow > 3) && (%ballThrow <= 6 )) {
n772=        inc %score 20
n773=      } 
n774=      else {
n775=        // you rolled a 30 then
n776=        inc %score 30
n777=      }
n778=    } 
n779=    inc %x
n780=  }
n781=
n782=  // now check the high score
n783=  var %highscore  $readini(%skeeballFile, n, highscore, highscore)
n784=  var %user  $readini(%skeeballFile, n, highscore, user)
n785=
n786=  if (%score > %highscore) {
n787=    writeini %skeeballFile highscore user $nick
n788=    writeini %skeeballFile highscore highscore %score
n789=    writeMsg $+(Congrats! You got the new high score of:, $chr(32), %score, !) 
n790=  } 
n791=  else {
n792=    writeMsg $+(You rolled a, $chr(32), %score, $chr(32),. Current highschore is,$chr(32), %highscore,$chr(32), by,$chr(32), %user) 
n793=  }
n794=}
n795=
n796=/*
n797=- play 18 holes
n798=- each hole has a positive and negative chance weighted on drunkedness and intelligence
n799=- negatives are +3, +2, +1
n800=- positives are 0, -1, -2
n801=*/
n802=alias -l playGoldenTee {
n803=  var %goldenTeeFile $+(%baseDir,\objects\goldentee.dickssportsbar.ini)
n804=  var %userFile $+(%baseDir,\users\,$nick,.ini)
n805=  var %drunk  $readini(%userFile, n, character, drunk)
n806=  var %intelligence $readini(%userFile, n, character, intelligence)
n807=  var %chance $calc(100 + %drunk - %intelligence)
n808=
n809=  // there is a cap on how much your intelligence affects you
n810=  // there should always be a chance at a negative
n811=  if ( %chance < 51) var %chance 51
n812=
n813=  var %score 0
n814=  // play 18 holes
n815=  var %x 1
n816=  while (%x <= 18) {
n817=    var %random $rand(1, %chance)
n818=    var %goldRandom $rand(1, 10)
n819=
n820=    if (%random <= 50) {
n821=      // this is a positive outcome
n822=      // golfRandom score meanings: 1-6 = 0, 7-9 = -1, 10 = -2
n823=      if (%goldRandom <= 6) {
n824=        // do nothing
n825=      } 
n826=      elseif ((%goldRandom > 6) && (%goldRandom <= 9)) {
n827=        dec %score 1
n828=      } 
n829=      else {
n830=        // you rolled an eagle then!
n831=        dec %score 2
n832=      }
n833=    } 
n834=    else {
n835=      // this is a negative throw
n836=      // goldRandom score meanings: 1-4 = 1, 5-8=2, 9-10 = 3
n837=      if (%goldRandom <= 4 ) {
n838=        inc %score 1
n839=      } 
n840=      elseif ((%goldRandom > 4) && (%goldRandom <= 8 )) {
n841=        inc %score 2
n842=      }
n843=      else {
n844=        // you get a triple boggie
n845=        inc %score 3
n846=      }
n847=    } 
n848=    inc %x
n849=  }
n850=
n851=  // now check the high score
n852=  var %highscore  $readini(%goldenTeeFile , n, highscore, highscore)
n853=  var %user  $readini(%goldenTeeFile , n, highscore, user)
n854=
n855=  if (%score < %highscore) {
n856=    writeini %goldenTeeFile highscore user $nick
n857=    writeini %goldenTeeFile highscore highscore %score
n858=    writeMsg $+(Congrats! You got the new high score of:, $chr(32), %score, !) 
n859=  } 
n860=  else {
n861=    writeMsg $+(You got a, $chr(32), %score, $chr(32),. Current highschore is,$chr(32), %highscore,$chr(32), by,$chr(32), %user) 
n862=  }
n863=}
n864=
n865=/*
n866=- hit the bag one time
n867=- weighted on strength + drunk
n868=- regular hits have a chance from 200-800 (200-500 if negative, 501-800 if positive)
n869=- if strenght + drunk > 100 chances go 200-1000
n870=*/
n871=alias -l playPunchingBag {
n872=  var %punchingBag $+(%baseDir,\objects\punchingbag.dickssportsbar.ini)
n873=  var %userFile $+(%baseDir,\users\,$nick,.ini)
n874=  var %drunk  $readini(%userFile, n, character, drunk)
n875=  var %strength $readini(%userFile, n, character, strength)
n876=  inc %strength %drunk
n877=
n878=  var %chance $calc(100 + %strength)
n879=
n880=  // if strength > 100 you can max to 1000, otherwise 800
n881=  var %maxHit 800
n882=  if ( %strength > 100) var %maxHit 1000
n883=
n884=  var %random $rand(1, %chance)
n885=  var %score 0
n886=  if (%random <= 50) {
n887=    // positive so go from 501-max
n888=    var %score $rand(501, %maxHit)
n889=  } 
n890=  else {
n891=    // negative hit so go from 200-500
n892=    var %score $rand(200,500)
n893=  }
n894=
n895=  // now check the high score
n896=  var %highscore  $readini(%punchingBag , n, highscore, highscore)
n897=  var %user  $readini(%punchingBag , n, highscore, user)
n898=
n899=  if (%score > %highscore) {
n900=    writeini %punchingBag highscore user $nick
n901=    writeini %punchingBag highscore highscore %score
n902=    writeMsg $+(Congrats! You got the new high score of:, $chr(32), %score, !) 
n903=  } 
n904=  else {
n905=    writeMsg $+(You got a, $chr(32), %score, $chr(32),. Current highschore is,$chr(32), %highscore,$chr(32), by,$chr(32), %user) 
n906=  }
n907=}
n908=
n909=/*
n910=- random chance. Stats dont matter
n911=- icons: bell, star, horseshoe, bar, cherry
n912=- odds per symbol: 20, 15, 10, 5, 1
n913=- payouts: bet, betx2, betx3, betx5, betx100
n914=- every 10 bets gets 1 beer free
n915=*/
n916=alias -l playSlots {
n917=  var %slotsFile $+(%baseDir,\objects\slots.queenofhearts.ini)
n918=  var %userFile $+(%baseDir,\users\,$nick,.ini)
n919=  var %bet $readini(%userFile , n, character, bet)
n920=
n921=  // handle the free drinks for gambling
n922=  var %slotPlays $readini(%userFile , n, character, slotPlays)
n923=
n924=  if (%slotPlays == $null ) {
n925=    var %slotPlays 1
n926=  } 
n927=  else {
n928=    inc %slotPlays 
n929=  }
n930=
n931=  if (%slotPlays >= %freeDrinkNumberSlots) {
n932=    writeMsg A waitress comes by and hands you a [lightbeer]
n933=    writeini %userFile character slotPlays 0
n934=    var %lightbeerFile $+(%baseDir,\objects\lightbeer.queenofhearts.ini)
n935=    addInventoryItem %lightbeerFile
n936=  }  
n937=  else {
n938=    writeini %userFile character slotPlays %slotPlays 
n939=  }
n940=
n941=  // get 3 random symbols
n942=  %x = 1
n943=  while (%x <= 3) {
n944=    var %random $rand(1,51)
n945=
n946=    if (%random <= 20) {
n947=      var %symbol. $+ %x 2
n948=      var %icon. $+ %x *bell*
n949=    } 
n950=    elseif ((%random > 20) && (%random <= 35)) {
n951=      var %symbol. $+ %x 5
n952=      var %icon. $+ %x *star*
n953=    }
n954=    elseif ((%random > 35) && (%random <= 45)) {
n955=      var %symbol. $+ %x 8
n956=      var %icon. $+ %x *horseshoe*
n957=    }
n958=    elseif ((%random > 45) && (%random <= 50)) {
n959=      var %symbol. $+ %x 10
n960=      var %icon. $+ %x *bar*
n961=    } 
n962=    else {
n963=      // this is the jackpot symbol!
n964=      var %symbol. $+ %x 100
n965=      var %icon. $+ %x *cherry*
n966=    }
n967=
n968=    inc %x
n969=  }
n970=
n971=  var %output $+(%icon.1,-,%icon.2,-,%icon.3,. )
n972=
n973=  // now lets see if we are a winner
n974=  if (( %symbol.1 == %symbol.2) && (%symbol.2 == %symbol.3)) {
n975=    var %winnings $calc(%bet * %symbol.1) 
n976=    if (%symbol.1 == 100) {
n977=      var %output $+(%output, You win the jackpot!!! Winnings: , %winnings)
n978=    } 
n979=    else {
n980=      var %output $+(%output, Winner! Winnings: , %winnings)    
n981=    }
n982=
n983=    // payout the winnings
n984=    var %cash $readini(%userFile , n, character, cash)
n985=    inc %cash %winnings
n986=    writeini %userFile character cash %cash
n987=  }
n988=  else {
n989=    var %output $+(%output, Sorry you didn't win. Try again!)      
n990=  }
n991=
n992=  writeMsg %output
n993=}
n994=
n995=/*
n996=- chance to hit blackjack on first hand increased with intelligence
n997=- chance = 100 + intelligence - drunk
n998=- 1-00 fail. 100+ win
n999=*/
n1000=alias -l playBlackjack {
n1001=
n1002=  var %blackjackFile $+(%baseDir,\objects\blackjack.queenofhearts.ini)
n1003=  var %userFile $+(%baseDir,\users\,$nick,.ini)
n1004=  var %bet $readini(%userFile , n, character, bet)
n1005=  var %drunk $readini(%userFile , n, character, drunk)
n1006=  var %intelligence $readini(%userFile , n, character, intelligence)
n1007=  var %cash $readini(%userFile , n, character, cash)
n1008=  var %blackJackHands $readini(%userFile , n, character, blackjackHands)
n1009=
n1010=  // first handle the free drinks at %freeDrinkNumber
n1011=
n1012=  if (%blackJackHands == $null ) {
n1013=    var %blackjackHands 1
n1014=  } 
n1015=  else {
n1016=    inc %blackjackHands
n1017=  }
n1018=
n1019=  if (%blackJackHands >= %freeDrinkNumber) {
n1020=    writeMsg A waitress comes by and hands you a [lightbeer]
n1021=    writeini %userFile character blackjackHands 0
n1022=    var %lightbeerFile $+(%baseDir,\objects\lightbeer.queenofhearts.ini)
n1023=    addInventoryItem %lightbeerFile
n1024=  }  
n1025=  else {
n1026=    writeini %userFile character blackjackHands %blackjackHands
n1027=  }
n1028=
n1029=  writeini %userFile Blackjack card 0
n1030=  writeini %userFile Blackjack total 0
n1031=  writeini %userFile DealerBlackjack card 0
n1032=  writeini %userFile DealerBlackjack total 0
n1033=
n1034=  // first check if user hits the bonus black jack
n1035=  var %chance $calc(100 - %drunk + %intelligence)
n1036=  var %chanceRoll $rand(1,%chance)
n1037=
n1038=  if (%chanceRoll > 100) {
n1039=    // if we had over 100 chance and then rolled over 100 insta winner!
n1040=
n1041=    var %winnings $calc(%bet * 3 / 2)
n1042=    var %winnings $round(%winnings, 2)
n1043=    inc %cash %winnings
n1044=    writeini %userFile character cash %cash
n1045=    writeMsg You hit a blackjack on the first hand! Winnings: %winnings
n1046=
n1047=    remini %userFile Blackjack
n1048=    remini %userFile DealerBlackjack
n1049=  } 
n1050=  else {
n1051=    // just play the game now
n1052=    // draw two cards. after the second card we have our total
n1053=
n1054=    var %total $drawCard(user)
n1055=    var %total $drawCard(user)
n1056=
n1057=    if (%total == 21) {
n1058=      var %winnings $calc(%bet * 3 / 2)
n1059=      var %winnings $round(%winnings, 2)
n1060=      inc %cash %winnings
n1061=      writeini %userFile character cash %cash
n1062=      writeMsg You hit a blackjack on the first hand! Winnings: %winnings
n1063=
n1064=      // clear out the cards
n1065=      remini %userFile BlackJack
n1066=      remini %userFile DealerBlackJack
n1067=    } 
n1068=    else {
n1069=      var %card.1 $readini(%userFile , n, Blackjack, 1)
n1070=      var %card.2 $readini(%userFile , n, Blackjack, 2)
n1071=
n1072=      writeMsg You have %card.1 and %card.2 for a total of %total
n1073=
n1074=      // draw two cards and get total score
n1075=      var %dealerTotal $drawCard(dealer)
n1076=      var %dealerTotal $drawCard(dealer)
n1077=
n1078=      if (%dealerTotal == 21) {
n1079=        writeMsg Dealer hit 21. You lose.
n1080=
n1081=        // clear out the cards
n1082=        remini %userFile BlackJack
n1083=        remini %userFile DealerBlackJack
n1084=      } 
n1085=      else {
n1086=        var %dealerCard.1 $readini(%userFile , n, DealerBlackjack, 1)
n1087=        var %dealerCard.2 $readini(%userFile , n, DealerBlackjack, 2)
n1088=
n1089=        writeMsg Dealer has %dealerCard.1 showing.
n1090=      }
n1091=    }
n1092=  }
n1093=}
n1094=
n1095=// pick a card then return the total
n1096=alias -l drawCard {
n1097=
n1098=  var %userFile $+(%baseDir,\users\,$nick,.ini)
n1099=  if ($1 == user) {
n1100=    // update for user
n1101=    var %section BlackJack
n1102=  } 
n1103=  else {
n1104=    // update for dealer
n1105=    var %section DealerBlackjack
n1106=  }
n1107=
n1108=  var %total  $readini(%userFile , n, %section, total)
n1109=  var %cardNumber $readini(%userFile , n, %section, card)
n1110=
n1111=  // draw a random card
n1112=  var %card $rand(2,14)
n1113=  // handle the cases where its a face card
n1114=  if (%card == 11) {
n1115=    var %cardPic J
n1116=    var %cardValue 10
n1117=  } 
n1118=  elseif (%card == 12) {
n1119=    var %cardPic Q
n1120=    var %cardValue 10
n1121=  }
n1122=  elseif (%card == 13) {
n1123=    var %cardPic K
n1124=    var %cardValue 10
n1125=  }
n1126=  elseif (%card == 14) {
n1127=    var %cardPic A
n1128=    var %cardValue 11
n1129=
n1130=    // handle the case where 11 puts the user over 21   
n1131=    var %tempTotal $calc(%total + 11)
n1132=    if (%tempTotal > 21 ) {
n1133=      var %cardValue 1
n1134=    }
n1135=  } 
n1136=  else {
n1137=    var %cardPic %card
n1138=    var %cardValue %card
n1139=  }
n1140=
n1141=  inc %total %cardValue
n1142=  inc %cardNumber
n1143=  writeini %userFile %section card %cardNumber
n1144=  writeini %userFile %section total %total
n1145=  writeini %userFile %section %cardNumber %cardPic
n1146=
n1147=  return %total
n1148=}
n1149=
n1150=// draw a card and see if win or bust
n1151=alias -l blackjackHit {
n1152=  var %userFile $+(%baseDir,\users\,$nick,.ini)
n1153=  var %bet $readini(%userFile , n, character, bet)
n1154=  var %cash $readini(%userFile , n, character, bet)
n1155=
n1156=
n1157=  var %total $drawCard(user)
n1158=
n1159=  if (%total == 21) {
n1160=    var %winnings $calc(%bet * 3 / 2)
n1161=    var %winnings $round(%winnings, 2)
n1162=    inc %cash %winnings
n1163=    writeini %userFile character cash %cash 
n1164=    writeMsg You hit a blackjack! Winnings: %winnings
n1165=
n1166=    // clear out the cards
n1167=    remini %userFile BlackJack
n1168=    remini %userFile DealerBlackJack
n1169=  } 
n1170=  elseif (%total > 21) {
n1171=    writeMsg You busted with a total of %total
n1172=
n1173=    remini %userFile BlackJack
n1174=    remini %userFile DealerBlackJack
n1175=  } 
n1176=  else {
n1177=    var %cards $readini(%userFile , n, BlackJack, card)
n1178=    var %x 1
n1179=    var %output You have  
n1180=    while (%x <= %cards) {
n1181=      var %card. $+ %x $readini(%userFile , n, BlackJack, %x)
n1182=      var %cardPic $($+(%,card.,%x),2)
n1183=      var %output $+(%output,$chr(32),%cardPic, $chr(32)) 
n1184=      inc %x
n1185=    }
n1186=    var %output $+(%output,$chr(32),For a total of, $chr(32), %total) 
n1187=    writeMsg %output
n1188=  } 
n1189=}
n1190=
n1191=alias -l blackjackStand {
n1192=  var %userFile $+(%baseDir,\users\,$nick,.ini)
n1193=  var %bet $readini(%userFile , n, character, bet)
n1194=  var %cash $readini(%userFile , n, character, cash)
n1195=  var %total $readini(%userFile , n, DealerBlackJack, total)
n1196=  var %userTotal $readini(%userFile , n, BlackJack, total)
n1197=
n1198=  while (%total < 17) {
n1199=    var %total $drawCard(dealer)
n1200=  }
n1201=
n1202=  if ((%total > %userTotal) && (%total <= 21)) {
n1203=    writeMsg You lose. Dealer hit %total
n1204=  } 
n1205=  elseif (%total > 21) {
n1206=    var %winnings $calc(%bet * 3 / 2)
n1207=    var %winnings $round(%winnings, 2)
n1208=    inc %cash %winnings
n1209=    writeini %userFile character cash %cash 
n1210=    writeMsg You win! Dealer busts with %total $+ ! Winnings: %winnings
n1211=
n1212=  } 
n1213=  else {
n1214=    //it's a push then
n1215=    inc %cash %bet
n1216=    writeini %userFile character cash %cash 
n1217=
n1218=    writeMsg Push. You get your bet back. 
n1219=  }
n1220=
n1221=  // clear out the cards
n1222=  remini %userFile BlackJack
n1223=  remini %userFile DealerBlackJack
n1224=}
n1225=
n1226=// do a check to see if we have room to add an item
n1227=alias -l checkInventorySpace {
n1228=  var %userFile $+(%baseDir,\users\,$nick,.ini)
n1229=  var %items $readini(%userFile , n, character, items)
n1230=
n1231=  if (%items < %inventoryMax) {
n1232=    return $true
n1233=  } 
n1234=  else {
n1235=    writeMsg You don't have anymore room in your inventory
n1236=    return $false
n1237=  }
n1238=}
n1239=
n1240=// really sloppy, found out about the token commands addtok, gettok, etc after doing all this
n1241=// oh well 
n1242=alias -l parseCommand {
n1243=  var %count $count($1-, $chr(32))
n1244=  var %x 1
n1245=  var %lastPos 1 
n1246=  var %string $1-
n1247=
n1248=  // first loop through and get all the space locations
n1249=  while (%x <= %count) {
n1250=    // get the xth occurence of space
n1251=    var %pos. $+ %x $pos($1-, $chr(32),%x)
n1252=    inc %x
n1253=  }
n1254=
n1255=  var %x 1
n1256=  var %lastPos 1
n1257=  while (%x <= %count) {
n1258=    var %thisPos $($+(%,pos.,%x),2)
n1259=    var %length $calc(%thisPos - %lastPos)
n1260=    var %sub $mid(%string, %lastPos, %length)
n1261=    var %lastPos %thisPos
n1262=
n1263=    var %words. $+ %x %sub
n1264=    inc %x
n1265=  }
n1266=
n1267=  // get the last word
n1268=  var %length $calc($len(%string) - %lastPos + 1)
n1269=  var %sub $mid(%string, %lastPos, %length)
n1270=  var %words. $+ %x %sub
n1271=
n1272=  // now that we have a list of words, loop through and check if they match our filtered words
n1273=  // there is one more word than spaces to incremement count
n1274=  var %x 1
n1275=  inc %count
n1276=  var %found 0
n1277=  var %return
n1278=
n1279=  while (%x <= %count) {
n1280=    var %thisWord $($+(%,words.,%x),2)
n1281=    var %thisWord $remove(%thisWord, $chr(32))
n1282=
n1283=    if ((%thisWord == a) || (%thisWord == at) || (%thisWord == with) || (%thisWord == the) || (%thisWord == on) || (%thisWord == it) || (%thisWord == an) || (%thisWord == to)) {
n1284=      // skip this word
n1285=    }
n1286=    else {
n1287=      if (%found == 0) {
n1288=        var %return %thisWord
n1289=      } 
n1290=      else {
n1291=        var %return $+(%return, $chr(32), %thisWord)
n1292=      }
n1293=      inc %found
n1294=    }  
n1295=    inc %x 
n1296=  }
n1297=  return %return 
n1298=}
n1299=
n1300=/*
n1301=death = chance roll 100
n1302=roll 1-21
n1303=drunk < 50 chance to triple
n1304=drunk < 75 chance to double
n1305=*/
n1306=alias -l throwDart {
n1307=  var %userFile $+(%baseDir,\users\,$nick,.ini)
n1308=  var %drunk $readini(%userFile , n, character, drunk)
n1309=  var %output You hit a 
n1310=
n1311=  var %random $rand(1, 21)
n1312=
n1313=  if (%random <= 20) {
n1314=    var %hit %random
n1315=  }
n1316=  else {
n1317=    // bullseye
n1318=    var %hit 25
n1319=  }
n1320=  var %multiplyChance $rand(1,10)  
n1321=
n1322=  if (%drunk < 50) {
n1323=    if (%multiplyChance <= 2) {
n1324=      // you hit a double
n1325=      if (%hit == 25) {
n1326=        // don't matter, need a 3 to hit a double bullseye
n1327=      } 
n1328=      else {
n1329=        var %output $+(%output, $chr(32), double, $chr(32), %hit, $chr(32), for)
n1330=        inc %hit %hit
n1331=      }
n1332=    }
n1333=    elseif (%multiplyChance == 3) {
n1334=      // you hit a triple
n1335=      if (%hit == 25) {
n1336=        var %output $+(%output, $chr(32), double bullseye, $chr(32), for)
n1337=        inc %hit %hit
n1338=      } 
n1339=      else {
n1340=        var %output $+(%output, $chr(32), triple, $chr(32), %hit, $chr(32), for)
n1341=        var %hit $calc(%hit * 3)
n1342=      }
n1343=    }
n1344=  } 
n1345=  elseif (%drunk < 75) {
n1346=    if (%multiplyChance <= 2) {
n1347=      // you hit a double
n1348=      if (%hit == 25) {
n1349=        // skip must be less than 50 drunk and hit a 3 on the chance roll
n1350=      } 
n1351=      else {
n1352=        var %output $+(%output, $chr(32), double, $chr(32), %hit, $chr(32), for)
n1353=        inc %hit %hit
n1354=      }
n1355=    }
n1356=  } 
n1357=  else {
n1358=    // 10% chance of killing yourself with a dart
n1359=    if (%multiplyChance == 1) {
n1360=      var %objFile $+(%baseDir,\objects\darts.murphys.ini)
n1361=      var %deathMessage $readini(%objFile , n, object, deathMessage)
n1362=      var %output %deathMessage
n1363=      var %death = $true
n1364=    }
n1365=  }
n1366=  if (%death != $null) {
n1367=    writeMsg %output
n1368=    killCharacter dart to face
n1369=  } 
n1370=  else {
n1371=    var %output $+(%output,$chr(32), %hit)
n1372=    writeMsg %output
n1373=  }
n1374=}
n1375=
n1376=/*
n1377=All commands list here
n1378=*/
n1379=on *:TEXT:!*:#: { 
n1380=  // almost all comands need the user file so just set that up here
n1381=  var %userFile $+(%baseDir,\users\,$nick,.ini)
n1382=
n1383=  // do some logic to remove words like at, the, with, etc.
n1384=  if ($2 != $null) {
n1385=    var %words $parseCommand($1-)
n1386=    tokenize 32 %words
n1387=  }
n1388=
n1389=  if ($1 == !startGame) {
n1390=    writeMsg $+(You joined,$chr(32),%gameTitle,.,$chr(32),%welcomeMessage)
n1391=    var %alive  $readini(%userFile , n, character, alive)
n1392=
n1393=    if (%alive == yes) {
n1394=      writeMsg Welcome back! Your are continuing where you last left off.   
n1395=    } 
n1396=    else {
n1397=      writeMsg %rollCharacterMessage
n1398=    }
n1399=    // turn on the switch to indicate the user is in the game
n1400=    writeini %userFile character ingame yes
n1401=
n1402=  } 
n1403=  elseif ($1 == !class) {
n1404=    if ($checkUser) {
n1405=      listClasses $2
n1406=    }
n1407=  }
n1408=  elseif ($1 == !roll) {
n1409=    if ($checkUser) {
n1410=      if ($2 == $null) {
n1411=        writeMsg please specify a class to roll
n1412=      } 
n1413=      else {
n1414=        setClass $2
n1415=      }
n1416=    }
n1417=  }
n1418=  elseif ($1 == !dance) {
n1419=    if ($checkUser) {
n1420=      if ($2 == $null) {
n1421=        writeMsg You do a little dance
n1422=      } 
n1423=      else {
n1424=        var %loc $readini(%userFile , n, character, location)
n1425=        var %objFile $+(%baseDir,\objects\,$2,.,%loc,.ini)
n1426=        var %danceable $readini(%objFile, n, object, danceable)
n1427=
n1428=        if ((%danceable == $null) || (%danceable == no)) {
n1429=          writeMsg You can't dance on that!        
n1430=        } 
n1431=        elseif (%danceable == yes) {
n1432=          var %use $readini(%objFile, n, object, use)
n1433=          writeMsg %use
n1434=        } 
n1435=        else {
n1436=          // should never get here
n1437=        } 
n1438=
n1439=      }
n1440=    }
n1441=  }
n1442=  elseif ($1 == !stopGame) {
n1443=    if ($checkUser) {
n1444=      writeMsg $+(You left,$chr(32),%gameTitle)
n1445=
n1446=      // turn off the switch to indicate the user is no longer in the game
n1447=      writeini %userFile character ingame no  
n1448=    }
n1449=  } 
n1450=  elseif ($1 == !addCash) {
n1451=    if ($nick isop %channel) {
n1452=      addCash $2 $3
n1453=    } 
n1454=    else {
n1455=      writeMsg Sneaky! Sneaky! You must be an admin to use this command.
n1456=    }
n1457=  } 
n1458=  elseif ($1 == !locations) {
n1459=    if ($checkUser) {
n1460=      var %loc $readini(%userFile , n, character, location)
n1461=
n1462=      if (%loc == Street) {
n1463=        listLocations        
n1464=      } 
n1465=      else {
n1466=        writeMsg You must be outside to see the locations. Use !go Street to go outside
n1467=      }
n1468=    }  
n1469=  }
n1470=  elseif ($1 == !go ) {
n1471=    if ($checkUser) {
n1472=      if ($2 == $null) {
n1473=        writeMsg You must specify a place to travel to!
n1474=      } 
n1475=      else {
n1476=        var %loc $readini(%userFile , n, character, location)
n1477=
n1478=        if  (%loc == Street) {
n1479=          travelTo $2 $3
n1480=        } 
n1481=        else {
n1482=          if ($2 == Street) { 
n1483=            travelTo Street
n1484=          } 
n1485=          else {
n1486=            writeMsg You must be outside to travel to a new location. Use !go Street to go outside.
n1487=          }
n1488=        }
n1489=      }
n1490=    }
n1491=  } 
n1492=  elseif ($1 == !look) {
n1493=    if ($checkUser) {
n1494=      var %loc $readini(%userFile , n, character, location)
n1495=
n1496=      if ($2 == $null) {
n1497=        var %description $readini(%listFile , n, %loc, description)
n1498=        writeMsg %description
n1499=      } 
n1500=      else {
n1501=        var %objFile $+(%baseDir,\objects\,$2,.,%loc,.ini)
n1502=        var %description  $readini(%objFile, n, object, description)
n1503=        if (%description == $null) {
n1504=          writeMsg $+(Can't find object:, $chr(32), $2)
n1505=        } 
n1506=        else {
n1507=          writeMsg %description
n1508=        }
n1509=      }
n1510=    }
n1511=  }
n1512=  elseif ($1 == !buy) {
n1513=    if ($checkUser) {
n1514=      if ($2 == $null) {
n1515=        writeMsg You must specify an item to buy
n1516=      } 
n1517=      else {
n1518=        var %loc $readini(%userFile , n, character, location)
n1519=        var %objFile $+(%baseDir,\objects\,$2,.,%loc,.ini)
n1520=        var %price $readini(%objFile, n, object, price)
n1521=
n1522=        if (%price == $null) {
n1523=          writeMsg $+(Can't find object:, $chr(32), $2)
n1524=        } 
n1525=        else {
n1526=          if ($checkInventorySpace) {
n1527=            var %paid $useMoney(%price)
n1528=
n1529=            if (%paid) {
n1530=              addInventoryItem %objFile
n1531=              writeMsg You purchased: $2
n1532=            } 
n1533=          }
n1534=        }
n1535=      }
n1536=    }
n1537=  }
n1538=  elseif ($1 == !inventory) {
n1539=    if ($checkUser) {
n1540=      var %invCount $readini(%userFile , n, character, items)
n1541=      var %output Inventory: 
n1542=      var %i 1
n1543=
n1544=      while (%i <= %invCount ) {
n1545=        var %itemFile $readini(%userFile , n, inventory, %i)
n1546=        var %name $readini(%itemFile, n, object, name)
n1547=
n1548=        // some logic to take care of adding the commas
n1549=        if (%i == 1 ) {
n1550=          var %output $+(%output,$chr(32),%name)
n1551=        } 
n1552=        else {
n1553=          var %output $+(%output,$chr(44),$chr(32),%name)
n1554=        }
n1555=
n1556=        inc %i
n1557=      }
n1558=
n1559=      writeMsg %output
n1560=    }
n1561=  } 
n1562=  elseif ($1 == !drink) {
n1563=    if ($checkUser) {
n1564=      if ($2 == $null) {
n1565=        writeMsg What do you want to drink? Use !drink BEVERAGE
n1566=      } 
n1567=      else {
n1568=        var %itemFile $getItem($2)
n1569=        if (%itemFile != $null) {
n1570=          drink %itemFile
n1571=        } 
n1572=        else {
n1573=          writeMsg $+(Can't find, $chr(32), $2, $chr(32), in your inventory) 
n1574=        }
n1575=      }
n1576=    }
n1577=  }
n1578=  elseif ($1 == !eat) {
n1579=    if ($checkUser) {
n1580=      if ($2 == $null) {
n1581=        writeMsg What do you want to eat? Use !eat FOOD
n1582=      } 
n1583=      else {
n1584=        var %itemFile $getItem($2)
n1585=        if (%itemFile != $null) {
n1586=          eat %itemFile
n1587=        } 
n1588=        else {
n1589=          writeMsg $+(Can't find, $chr(32), $2, $chr(32), in your inventory) 
n1590=        }
n1591=      }
n1592=    }
n1593=  }
n1594=  elseif ($1 == !drop) {
n1595=    if ($checkUser) {
n1596=      if ($2 == $null) {
n1597=        writeMsg What do you want to drop? Use !drop ITEM
n1598=      } 
n1599=      else {
n1600=        var %itemFile $getItem($2)
n1601=        if (%itemFile != $null) {
n1602=          removeItem %itemFile
n1603=          writeMsg You drop: $2
n1604=        } 
n1605=        else {
n1606=          writeMsg $+(Can't find, $chr(32), $2, $chr(32), in your inventory) 
n1607=        }
n1608=      }
n1609=    }
n1610=  }
n1611=  elseif ($1 == !bet) {
n1612=    if ($checkUser) {
n1613=      if (($2 != $null) && ($2 isnum)) {
n1614=        if ($2 > 0) {
n1615=          // handle the case where someone tries to chance their bet after getting drawn cards         
n1616=          var %blackjackCard $readini(%userFile , n, BlackJack, card)
n1617=
n1618=          if (%blackjackCard == $null) {
n1619=            var %bet $round($2, 2)
n1620=
n1621=            writeini %userFile character bet %bet 
n1622=            writeMsg You are now betting %bet for all gambling games
n1623=          } 
n1624=          else {
n1625=            writeMsg You cannot change your bet in the middle of a hand
n1626=          }
n1627=        } 
n1628=        else {
n1629=          writeMsg You must bet at least a dollar!
n1630=        }
n1631=      } 
n1632=      else {
n1633=        writeMsg You must specify a number to bet
n1634=      }
n1635=    }
n1636=  }
n1637=  elseif ($1 == !me) {
n1638=    if ($checkUser) {
n1639=      var %class $readini(%userFile , n, character, class)
n1640=      var %loc $readini(%userFile , n, character, location)
n1641=      var %sex $readini(%userFile , n, character, sex)
n1642=      var %strength $readini(%userFile , n, character, strength)
n1643=      var %appearance $readini(%userFile , n, character, appearance)
n1644=      var %intelligence $readini(%userFile , n, character, intelligence)
n1645=      var %tolerance $readini(%userFile , n, character, tolerance)
n1646=      var %cash $readini(%userFile , n, character, cash)
n1647=      var %drunk $readini(%userFile , n, character, drunk)
n1648=      var %bet $readini(%userFile , n, character, bet)
n1649=
n1650=      writeMsg Class: %class Sex: %sex Strength: %strength Intelligence: %intelligence Appearance: %appearance Tolerance: %tolerance Drunk: %drunk Cash: %cash Betting Amount: %bet Location: %loc
n1651=    }
n1652=  }
n1653=  elseif ($1 == !hit) {
n1654=    var %cards $readini(%userFile , n, Blackjack, card)
n1655=    if (%cards == $null) {
n1656=      writeMsg You must start a game of blackjack first. !play BlackJack
n1657=    } 
n1658=    else {
n1659=      blackjackHit
n1660=    }
n1661=
n1662=  }
n1663=  elseif ($1 == !stand) {
n1664=    var %cards $readini(%userFile , n, Blackjack, card)
n1665=    if (%cards == $null) {
n1666=      writeMsg You must start a game of blackjack first. !play BlackJack
n1667=    } 
n1668=    else {
n1669=      blackjackStand
n1670=    }
n1671=  }
n1672=  elseif ($1 == !play) {
n1673=    if ($checkUser) {
n1674=      if ($2 == $null) {
n1675=        writeMsg Play with what?
n1676=      } 
n1677=      else {
n1678=        var %loc $readini(%userFile , n, character, location)
n1679=        var %objFile $+(%baseDir,\objects\,$2,.,%loc,.ini)
n1680=        var %description  $readini(%objFile, n, object, description)
n1681=        if (%description == $null) {
n1682=          writeMsg $+(Can't find object:, $chr(32), $2)
n1683=        } 
n1684=        else {
n1685=          if ($2 == Jenga) {
n1686=            if ($3 == $null) {
n1687=              writeMsg You have to challenge someone. Use !play Jenga PERSON
n1688=            } 
n1689=            else {
n1690=              playChallengedGame Jenga $3 %loc %objFile 
n1691=            }
n1692=          } 
n1693=          elseif ($2 == Skeeball) {
n1694=            var %skeeballFile $+(%baseDir,\objects\skeeball.thedive.ini)
n1695=            var %price $readini(%skeeballFile , n, object, price)
n1696=
n1697=            var %paid $useMoney(%price)
n1698=            if (%paid) {
n1699=              playSkeeball
n1700=            }
n1701=          } 
n1702=          elseif ($2 == GoldenTee) {
n1703=            var %goldenTeeFile $+(%baseDir,\objects\goldentee.dickssportsbar.ini)
n1704=            var %price $readini(%goldenTeeFile , n, object, price)
n1705=
n1706=            var %paid $useMoney(%price)
n1707=            if (%paid) {
n1708=              playGoldenTee
n1709=            }
n1710=          }
n1711=          elseif ($2 == PunchingBag) {
n1712=            var %punchingBagFile $+(%baseDir,\objects\punchingbag.dickssportsbar.ini)
n1713=            var %price $readini(%punchingBagFile, n, object, price)
n1714=
n1715=            var %paid $useMoney(%price)
n1716=            if (%paid) {
n1717=              playPunchingBag
n1718=            }
n1719=          }
n1720=          elseif ($2 == slots) {
n1721=            var %slotsFile $+(%baseDir,\objects\slots.queenofhearts.ini)
n1722=            var %bet $readini(%userFile , n, character, bet)
n1723=
n1724=            var %paid $useMoney(%bet)
n1725=            if (%paid) {
n1726=              playSlots
n1727=            }
n1728=          }
n1729=          elseif ($2 == blackjack) {
n1730=            var %blackjackFile $+(%baseDir,\objects\blackjack.queenofhearts.ini)
n1731=            var %bet $readini(%userFile , n, character, bet)
n1732=
n1733=            var %paid $useMoney(%bet)
n1734=            if (%paid) {
n1735=              playBlackjack
n1736=            }
n1737=          }
n1738=          elseif ($2 == darts) {
n1739=            throwDart
n1740=          }
n1741=        }
n1742=      }
n1743=    }
n1744=  }
n1745=  else {
n1746=    // do nothing   
n1747=  }
n1748=}

[variables]
